//やっていることはSample35_奇数偶数分けその2共有メモリサンプルのタイムスタンプ可視化その2.hspにすごい似ています。
//同じkernelを何度も無駄実行するサンプルですが、前は実行が逐次でした。
//今度はHCLSetCommandQueueで実行するCommandQueue番号を変えることでオーバーラップ実行させることが可能になります。
//デフォルトで1deviceあたり切り替えられるCommandQueueは4つまであります。
#include "HSPCL64.as"
#include "ViewTimeStamp_module.as"
	randomize
	HCLinit
	
	mes "65536個のスレッドを実行します。\n各々のスレッドで64個の数値を偶数奇数カウントしていきます。\nこのときカーネル内で256個のスレッドが同期して情報を共有します\n"
	dim rando,65536*64
		repeat 65536*64;419万個のランダム数生成
		rando.cnt=rnd(32767)*rnd(32000)+rnd(32767)
		loop
	mes "65536*64個のランダム数生成"

	mes "ソースコンパイル"
	prgid=HCLCreateProgram("奇数偶数分けその2共有メモリサンプル.cl")
	krnid=HCLCreateKernel(prgid,"wake")
	wait 1

	mes "VRAMメモリ確保"
	vram=HCLCreateBuffer(65536*64*4)//65536*64のint型配列を作成
	HCLWriteBuffer vram,rando,65536*64*4,,,,0//event id 0
	kisu=HCLCreateBuffer(256*4);奇数の数を保存するchar型配列変数を作成
	gusu=HCLCreateBuffer(256*4);偶数の数を保存するchar型配列変数を作成
	HCLSetKernel krnid,0,vram;
	HCLSetKernel krnid,1,0,4*256;奇数用ローカルメモリ作成。１ワークグループにつき1024byte作成
	HCLSetKernel krnid,2,0,4*256;偶数用ローカルメモリ作成。１ワークグループにつき1024byte作成
	HCLSetKernel krnid,3,kisu;
	HCLSetKernel krnid,4,gusu;

	//GPU側で計算開始！
	mes "カーネル実行中"
	//無駄に多く実行。可視化してみるのが目的なので
	HCLSetCommandQueue 0
	HCLDoKrn1 krnid,65536*64/64,256,1//event id 1
	HCLDoKrn1 krnid,65536*64/64,256,2//event id 2
	HCLDoKrn1 krnid,65536*64/64,256,3//event id 3
	HCLDoKrn1 krnid,65536*64/64,256,4//event id 4
	HCLDoKrn1 krnid,65536*64/64,256,5//event id 5
	HCLDoKrn1 krnid,65536*64/64,256,6//event id 6
	HCLSetCommandQueue 1
	HCLDoKrn1 krnid,65536*64/64,256,7//event id 7
	HCLDoKrn1 krnid,65536*64/64,256,8//event id 8
	HCLDoKrn1 krnid,65536*64/64,256,9//event id 9
	HCLDoKrn1 krnid,65536*64/64,256,10//event id 10
	HCLDoKrn1 krnid,65536*64/64,256,11//event id 11
	HCLSetCommandQueue 2
	HCLDoKrn1 krnid,65536*64/64,256,12//event id 12
	HCLDoKrn1 krnid,65536*64/64,256,13//event id 13
	HCLDoKrn1 krnid,65536*64/64,256,14//event id 14
	HCLDoKrn1 krnid,65536*64/64,256,15//event id 15
	HCLDoKrn1 krnid,65536*64/64,256,16//event id 16
	HCLSetCommandQueue 3
	HCLDoKrn1 krnid,65536*64/64,256,17//event id 17
	HCLDoKrn1 krnid,65536*64/64,256,18//event id 18
	HCLDoKrn1 krnid,65536*64/64,256,19//event id 19
	HCLDoKrn1 krnid,65536*64/64,256,20//event id 20
	HCLFlush//*1下記に注釈


	HCLSetCommandQueue 0
	HCLFinish//HCLFinishはコマンドキューごとに実行する必要がある
	HCLSetCommandQueue 1
	HCLFinish
	HCLSetCommandQueue 2
	HCLFinish
	HCLSetCommandQueue 3
	HCLFinish
	mes "カーネル実行終了"
	
	//CPU側で集計
	dim k,256
	HCLReadBuffer kisu,k,256*4,,,,21//event id 21
	奇数の数=0
		repeat 256
		奇数の数+=k.cnt
		loop
	HCLReadBuffer gusu,k,256*4,,,,22//event id 22
	偶数の数=0
		repeat 256
		偶数の数+=k.cnt
		loop

	//表示
	mes "\nGPUでカウントした結果"
	mes "奇数の数		"+奇数の数
	mes "偶数の数		"+偶数の数
	mes "奇数の数＋数の数＝	"+(偶数の数+奇数の数)+"    =4194304なら正確にできた"


	//CPU側で計算開始
	repeat 65536*64;419万個ループ
		if rando.cnt\2:intkisuko++:else:intgusuko++
	loop

	//表示
	mes "\nCPUでカウントした結果"
	mes "奇数の数		"+intkisuko
	mes "偶数の数		"+intgusuko
	mes "奇数の数＋偶数の数＝	"+(intkisuko+intgusuko)+"    =4194304なら正確にできた"

	//タイムスタンプ可視化
	screen 1,640,480,0
	ViewEvents3 23
	stop


//*1
//HCLDoKrn1やHCLWriteBufferのblockingフラグにnonBlockingを指定したときのような「非同期実行」は
//CPU側がこれらタスク(コマンド)をキューに入れたあと、GPUがアイドルならすぐ実行されるものと思われがちですがちょっと違います。
//GPUがアイドル状態でもなかなかタスクを実行してくれないことがあります。(なんでこんな仕様なのか?)
//HCLFlushはキューのコマンドを強制的にデバイスに発行する命令です。
//つまり投入したコマンドを早く実行しろ、と発破をかける命令です。
//HCLSetDeviceで指定しているデバイスのすべてのCommandQueueに対して行います。