#include "HSPCL64.as"
	win_x=640
	win_y=480
	screen 0,win_x,win_y
	mref vrm,66
	fdim m,4,4
	m.0.0=float(1.0):m.1.1=float(1.0):m.2.2=float(1.0):m.3.3=float(1.0)
	fdim movexyz,3
	
	HCLinit

	ray=HCLCreateBuffer(4*4*win_x*win_y)
	rpos=HCLCreateBuffer(4*4*win_x*win_y)
	tlen=HCLCreateBuffer(4*win_x*win_y)
	buf=HCLCreateBuffer(3*win_x*win_y)

	prgid=HCLCreateProgram("raycode.cl","-D WX="+win_x+" -D WY="+win_y+"")
	krnfirststep=HCLCreateKernel(prgid,"firststep")
	krnmain=HCLCreateKernel(prgid,"mainloop")
	krnren=HCLCreateKernel(prgid,"renderling")
	
	HCLSetKernel krnfirststep,0,ray
	HCLSetKrns krnmain,rpos,ray,tlen
	HCLSetKrns krnren,buf,rpos,ray,tlen

		repeat -1
			repeat 16
			HCLSetKernel krnfirststep,1+cnt,m.(cnt\4).(cnt/4)
			loop
			repeat 3
			HCLSetKernel krnmain,3+cnt,movexyz.cnt
			loop
		HCLFillBuffer tlen,float(0)
		HCLDoKrn1_sub krnfirststep,win_x*win_y,64
		HCLDoKrn1_sub krnmain,win_x*win_y,64
		HCLDoKrn1_sub krnren,win_x*win_y,64
		HCLReadBuffer buf,vrm
		await 16
		redraw 1
		gosub*fpsview
		gosub*KeyAndRnd
		loop



*fpsview
	flm++
	if ms!gettime(6):ms=gettime(6):fps=flm:flm=0
	pos 0,0:color 0,255,0:mes "fps="+fps+""
	return

//ƒL[“ü—ÍˆÚ“® and ƒ‰ƒ“ƒ_ƒ€‰ñ“]
*KeyAndRnd
	count++
	rndval=(count/100)*13489689146857+1348960
	rndval=abs(rndval)
	rndval\=16777216
	xr=rndval\256-128
	rndval/=256
	yr=rndval\256-128
	rndval/=256
	zr=rndval-128

	rspd=0.0001
	fdim m_dmy,4,4
	//x‰ñ“]
	fdim radmat44,4,4
	radmat44.0.0=float(1.0)
	radmat44.3.3=float(1.0)
	radmat44.1.1=float(cos(rspd*xr))
	radmat44.2.1=float(-sin(rspd*xr))
	radmat44.1.2=float(sin(rspd*xr))
	radmat44.2.2=float(cos(rspd*xr))
	mulmm m,radmat44,m_dmy
	
	//y‰ñ“]
	fdim radmat44,4,4
	radmat44.1.1=float(1.0)
	radmat44.3.3=float(1.0)
	radmat44.0.0=float(cos(rspd*yr))
	radmat44.2.0=float(sin(rspd*yr))
	radmat44.0.2=float(-sin(rspd*yr))
	radmat44.2.2=float(cos(rspd*yr))
	mulmm m_dmy,radmat44,m
	
	//z‰ñ“]
	fdim radmat44,4,4
	radmat44.2.2=float(1.0)
	radmat44.3.3=float(1.0)
	radmat44.0.0=float(cos(rspd*zr))
	radmat44.1.0=float(-sin(rspd*zr))
	radmat44.0.1=float(sin(rspd*zr))
	radmat44.1.1=float(cos(rspd*zr))
	mulmm m,radmat44,m_dmy
	memcpy m,m_dmy,4*4*4

	
	//ˆÚ“®
	getkey key,'W'
	if key:movexyz.2+=float(0.11)
	getkey key,'S'
	if key:movexyz.2-=float(0.11)
	
	getkey key,'D'
	if key:movexyz.0+=float(0.11)
	getkey key,'A'
	if key:movexyz.0-=float(0.11)

	getkey key,38
	if key:movexyz.1-=float(0.21)
	getkey key,40
	if key:movexyz.1+=float(0.11)
	movexyz.1+=float(0.05)
	return

#module
//a‚ª4*4,b‚ª4*4,c‚ª4*4
#deffunc mulmm array a,array b,array c
	repeat 4
	ccnt=cnt
		repeat 4
		cccnt=cnt
		c.cccnt.ccnt=float(0.0)
			repeat 4
			c.cccnt.ccnt+=a.cnt.ccnt*b.cccnt.cnt
			loop
		loop
	loop
	return

//a‚ª4*4,b‚ª4,c‚ª4
#deffunc mulmv array a,array b,array c
	repeat 4
	ccnt=cnt
	c.ccnt=float(0.0)
		repeat 4
		c.ccnt+=a.cnt.ccnt*b.cnt
		loop
	loop
	return
#global