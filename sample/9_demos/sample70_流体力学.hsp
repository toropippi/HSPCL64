#include "HSPCL64.as"
	HCLinit
	//HCLSetDevice 0

#const WX 128//格子x ここを変えた場合kabep.pngなど4枚の画像も調整する必要あり
#const WY 128//格子y ここを変えた場合kabep.pngなど4枚の画像も調整する必要あり
#const WSZ2 WX*WY/2
#const ipn 6//格子スクリーン拡大倍率
#const taba 4//粒子の線どうしの縦の間隔(ドット)
DT=float(0.5)//デルタtime、時間刻み
μ=float(0.00001)//粘性率
μDT=μ*DT
speed=float(0.45)
alpha=float(1.0/(1.0+4.0*μDT))
#const RYSnum WX*WY*ipn*ipn//全体粒子数
#const RYSloop 128//何stepで粒子の初期化が一周するか
#const RYS1stepnum RYSnum/RYSloop//粒子座標初期化時に一気に更新する粒子数

	screen 0,WX*ipn,WY*ipn
	mref gsel0vram,66

	global_size=WX,WY
	local_size=WX,1

	fdim kkx,WX*WY
	fdim kky,WX*WY
		repeat WX*WY
		kkx.cnt=float(0)
		loop
		repeat WY-1
		kkx(1+cnt*WX)=float(speed)
		loop
		repeat WY-1
		kkx(cnt*WX)=float(speed)
		loop
	//描画粒子関連
	gosub*RYS_Setting
	
	//OpenCL関連
	//コードコンパイル、kernel抽出
	gosub*CreateKernels
	//cl_bufferを作成
	gosub*BufferSetting
	//SetKernel
	gosub*KernelSetting
	//OpenCL関連ここまで



////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
;cip移流(壁のところでは参照はするが書き込みはしない)、	 f値とg(微分)値が同時に更新される
;3次関数の部分では第4/5引数の速度はf3を参照、1から3引数はf3、g1を入力、係数もf3とg1から決定、f4、g2を出力
;差分式では、f3とg2を参照
;f4とg3を出力
;ここで一段落


;→外力	 f値のみ更新f0→f1
;→発散f1参照
;→圧力sor(壁のところでは参照はするが書き込みはしない)
;→圧力射影	 f値のみ更新f1→f2
;→cip微分値反映(ニューグレード、ソースでは圧力の更新しか反映されてないが、外力も含めないとダメ、さっきg値が更新された時のf値から今のf値までどのくらいずれたか)g0→g1、f0とf2参照
;→拡散、これはf値のみの更新でいい。f2→f3、f2参照
;→　　→　はじめへ
*main
	HCLCopyBuffer YU,YUN
	HCLCopyBuffer YV,YVN
	HCLCopyBuffer YP,YPN
	gosub*粘性
	if flm\8==0:gosub*外力
	gosub*pressure
	gosub*rhs
	gosub*newgrad
	gosub*veloc
	gosub*粒子
	if flm\5==4:gosub*粒子復活
	if flm\8==0:gosub*表示
	gosub*dcip0
	if flm\8==0{
		getkey key,27:if key:end
		await 0
	}
	flm++
	goto*main

////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////メインルーチン////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
*粘性
	HCLCopyBuffer GXd0,YUN
	HCLCopyBuffer GYd0,YVN
		repeat limit(10000.0*μ,0,(WX+WY)/2)
		HCLDoKernel krn50,2,global_size,local_size
		HCLDoKernel krn51,2,global_size,local_size
		loop
	return

*外力
	mkey=key1
	stick key1,256
	if key1&256{
		imaxx=mousex
		imayy=mousey
		if mkey=0:omxx=imaxx:omyy=imayy//クリックした瞬間なら
		xsa=omxx-imaxx
		ysa=omyy-imayy
		if (xsa!0)|(ysa!0){
			HCLReadBuffer YUN,kkx,4*WX*WY
			HCLReadBuffer YVN,kky,4*WX*WY
				repeat 16
				wwwx=(imaxx/ipn+cnt\4)\WX
				wwwy=(imayy/ipn+cnt/4)\WY
				if peek(kbx,wwwx+wwwy*WX):kkx(wwwx+wwwy*WX)=kkx(wwwx+wwwy*WX)-float(0.013)*xsa
				if peek(kby,wwwx+wwwy*WX):kky(wwwx+wwwy*WX)=kky(wwwx+wwwy*WX)-float(0.013)*ysa
				loop
			HCLWriteBuffer YUN,kkx,4*WX*WY
			HCLWriteBuffer YVN,kky,4*WX*WY
		}
		omxx=imaxx
		omyy=imayy
	}
	return

*pressure
	HCLDoKernel krn22,2,global_size,local_size;div計算
	HCLCopyBuffer YP,YPN
	//圧力計算はコストが高いのでとにかく最適化
		repeat 48
		HCLDoKrn1 krn20,WSZ2,0
		HCLDoKrn1 krn21,WSZ2,0//ローカルワークサイズはOpenCL実装に任せることに
		loop
	return

*rhs
	HCLDoKernel krn3,2,global_size,local_size
	return

*newgrad
	HCLDoKernel krn00,2,global_size,local_size
	HCLDoKernel krn01,2,global_size,local_size
	HCLDoKernel krn02,2,global_size,local_size
return

*veloc
	HCLCopyBuffer YU,YUN
	HCLCopyBuffer YV,YVN
	HCLDoKernel krn4,2,global_size,local_size

	HCLCopyBuffer GXd0,GXU
	HCLCopyBuffer GYd0,GYU
	HCLCopyBuffer Yd0,YUN

	HCLCopyBuffer GXd1,GXV
	HCLCopyBuffer GYd1,GYV
	HCLCopyBuffer Yd1,YVN

	HCLCopyBuffer GXd2,GXP
	HCLCopyBuffer GYd2,GYP
	HCLCopyBuffer Yd2,YPN
	return

*粒子//粒子移動ルーチン
	HCLDoKrn1 ryuusi,RYSnum,local_size.0*local_size.1;
	return

*粒子復活
	//CPU→GPU転送バージョン
	//HCLWriteBuffer RYS,ryuusihspgawa,RYS1stepnum*2*4,RYS1stepnum*2*4*hktkais,0,1

	//GPU→GPU転送バージョン、速度で有利なためこちらを採用
	//コピー先メモリオブジェクトid , コピー元メモリオブジェクトid , コピーサイズ , コピー先オフセット , コピー元オフセット
	HCLCopyBuffer RYS,RYS_syoki,RYS1stepnum*2*4,RYS1stepnum*2*4*hktkais,0
	
	hktkais++
	hktkais\=RYSloop
	return

*表示
	HCLDoKrn1 kout0,3*WX*WY*ipn*ipn/4,local_size.0*local_size.1;
	HCLDoKrn1 kout,WX*ipn*WY*ipn,local_size.0*local_size.1;
	//HCLFinish
	HCLReadBuffer out,gsel0vram,3*WX*WY*ipn*ipn
	pos 0,0:color 255,100,100
	gsel 0
	redraw 1
	//秒がかわったら
	if mtim!=gettime(6){
		mtim=gettime(6)
		viewfps=flm-lastflm
		lastflm=flm
	}
	mes "fps"+viewfps
	redraw 0
	return

*dcip0
	HCLDoKernel krn10,2,global_size,local_size
	HCLDoKernel krn11,2,global_size,local_size
	HCLDoKernel krn12,2,global_size,local_size
	return


//粒子関連の初期設定
*RYS_Setting
	fdim ryuusihspgawa,RYS1stepnum*2//粒子発生初期位置x,yが格納されている
		repeat RYS1stepnum
		ryuusihspgawa(cnt*2)  =(float(1.940389)/taba)*(cnt/(WY*ipn/taba))
		ryuusihspgawa(cnt*2+1)=float((1.0*taba*cnt+0.5*taba+0.005)\(WY*ipn))
		loop
	//この粒子初期値をstepごとにRYSに書き込んでいく。このときCPU→GPUだと遅くなりそうなので予めGPUに転送しておく。
	//どうせ中身の数値は変わらないので
	RYS_syoki=HCLCreateBufferFrom(ryuusihspgawa)
	return


*CreateKernels
	prg=HCLCreateProgram("nabie.cl","-D WX="+WX+" -D WY="+WY+" -D ipn="+ipn+"")//BuildOpntionでOpenCLに#define送る
	krn00=HCLCreateKernel(prg,"newgrad")
	krn01=HCLCreateKernel(prg,"newgrad")
	krn02=HCLCreateKernel(prg,"newgrad")
	krn10=HCLCreateKernel(prg,"dcip0")
	krn11=HCLCreateKernel(prg,"dcip0")
	krn12=HCLCreateKernel(prg,"dcip0")
	krn50=HCLCreateKernel(prg,"nensei0")
	krn51=HCLCreateKernel(prg,"nensei1")
	krn20=HCLCreateKernel(prg,"pressure0")
	krn21=HCLCreateKernel(prg,"pressure1")
	krn22=HCLCreateKernel(prg,"div")
	krn3=HCLCreateKernel(prg,"rhs")
	krn4=HCLCreateKernel(prg,"veloc")
	kout=HCLCreateKernel(prg,"out")
	kout0=HCLCreateKernel(prg,"out0")
	firstSetting=HCLCreateKernel(prg,"firstSetting")
	ryuusi=HCLCreateKernel(prg,"ryuusi")
	return

*BufferSetting
	YU=HCLCreateBufferFrom(kky)
	YUN=HCLCreateBufferFrom(kky)
	GXU=HCLCreateBufferFrom(kky)
	GYU=HCLCreateBufferFrom(kky)

	YV=HCLCreateBufferFrom(kky)
	YVN=HCLCreateBufferFrom(kky)
	GXV=HCLCreateBufferFrom(kky)
	GYV=HCLCreateBufferFrom(kky)	

	GXd0=HCLCreateBufferFrom(kky)
	GYd0=HCLCreateBufferFrom(kky)
	Yd0=HCLCreateBufferFrom(kky)
	GXd1=HCLCreateBufferFrom(kky)
	GYd1=HCLCreateBufferFrom(kky)
	Yd1=HCLCreateBufferFrom(kky)
	GXd2=HCLCreateBufferFrom(kky)
	GYd2=HCLCreateBufferFrom(kky)
	Yd2=HCLCreateBufferFrom(kky)

	YP=HCLCreateBufferFrom(kky)
	YPN=HCLCreateBufferFrom(kky)
	GXP=HCLCreateBufferFrom(kky)
	GYP=HCLCreateBufferFrom(kky)

	YUT=HCLCreateBufferFrom(kky)
	YVT=HCLCreateBufferFrom(kky)
	YUV=HCLCreateBufferFrom(kky)
	YVU=HCLCreateBufferFrom(kky)
	DIV=HCLCreateBufferFrom(kky)

	kabeP=HCLCreateBuffer(WX*WY):HCLWriteBuffer kabeP,kky,WX*WY
	kabeX=HCLCreateBuffer(WX*WY):HCLWriteBuffer kabeX,kky,WX*WY
	kabeY=HCLCreateBuffer(WX*WY):HCLWriteBuffer kabeY,kky,WX*WY

	out=HCLCreateBuffer(WX*WY*ipn*ipn*3);画面
	RYS=HCLCreateBuffer(RYSnum*4*2)

	HCLSetKernel firstSetting,0,RYS
	HCLDoKrn1 firstSetting,RYSnum,local_size.0*local_size.1;

	HCLWriteBuffer YUN,kkx
	HCLWriteBuffer YVN,kky

	dim kbp,WX*WY/4
	dim kbx,WX*WY/4
	dim kby,WX*WY/4

	buffer 1:picload "kabep.png"
		repeat WX:a3=cnt:repeat WY
			pget a3,cnt:poke kbp,a3+cnt*WX,ginfo_r
		loop:loop

	buffer 1:picload "kabex.png"
		repeat WX:a3=cnt:repeat WY
			pget a3,cnt:poke kbx,a3+cnt*WX,ginfo_r
		loop:loop

	buffer 1:picload "kabey.png"
		repeat WX:a3=cnt:repeat WY
			pget a3,cnt:poke kby,a3+cnt*WX,ginfo_r
		loop:loop

	buffer 1:picload "kabew.png"
		repeat WX:a3=cnt:repeat WY
			pget a3,cnt
			if ginfo_r=0{
			poke kbp,a3+cnt*WX,0
			poke kbx,a3+cnt*WX,0
			poke kbx,(a3+1)\WX+cnt*WX,0
			poke kby,a3+cnt*WX,0
			poke kby,a3+((cnt+1)\WY)*WX,0
		}
		loop:loop

	HCLWriteBuffer kabeP,kbp,WX*WY
	HCLWriteBuffer kabeX,kbx,WX*WY
	HCLWriteBuffer kabeY,kby,WX*WY
	return

*KernelSetting
	HCLSetKernel krn50,0,YU
	HCLSetKernel krn50,1,YUN
	HCLSetKernel krn50,2,YV
	HCLSetKernel krn50,3,YVN
	HCLSetKernel krn50,4,GXd0
	HCLSetKernel krn50,5,GYd0
	HCLSetKernel krn50,6,μDT
	HCLSetKernel krn50,7,alpha
	HCLSetKernel krn50,8,kabeX
	HCLSetKernel krn50,9,kabeY

	HCLSetKernel krn51,0,YU
	HCLSetKernel krn51,1,YUN
	HCLSetKernel krn51,2,YV
	HCLSetKernel krn51,3,YVN
	HCLSetKernel krn51,4,GXd0
	HCLSetKernel krn51,5,GYd0
	HCLSetKernel krn51,6,μDT
	HCLSetKernel krn51,7,alpha
	HCLSetKernel krn51,8,kabeX
	HCLSetKernel krn51,9,kabeY

	HCLSetKernel krn22,1,YUN
	HCLSetKernel krn22,2,YVN
	HCLSetKernel krn22,0,DIV

	HCLSetKernel krn20,0,DIV
	HCLSetKernel krn20,1,YPN
	HCLSetKernel krn20,2,kabeP
	HCLSetKernel krn21,0,DIV
	HCLSetKernel krn21,1,YPN
	HCLSetKernel krn21,2,kabeP

	HCLSetKernel krn3,0,YUN
	HCLSetKernel krn3,1,YVN
	HCLSetKernel krn3,2,YPN
	HCLSetKernel krn3,3,kabeX
	HCLSetKernel krn3,4,kabeY

	HCLSetKernel krn02,0,YUN
	HCLSetKernel krn02,1,YU
	HCLSetKernel krn02,2,GXU
	HCLSetKernel krn02,3,GYU
	HCLSetKernel krn02,4,kabeX

	HCLSetKernel krn00,0,YVN
	HCLSetKernel krn00,1,YV
	HCLSetKernel krn00,2,GXV
	HCLSetKernel krn00,3,GYV
	HCLSetKernel krn00,4,kabeY

	HCLSetKernel krn01,0,YPN
	HCLSetKernel krn01,1,YP
	HCLSetKernel krn01,2,GXP
	HCLSetKernel krn01,3,GYP
	HCLSetKernel krn01,4,kabeP

	HCLSetKernel krn4,0,YU
	HCLSetKernel krn4,1,YV
	HCLSetKernel krn4,2,YVU
	HCLSetKernel krn4,3,YUV
	HCLSetKernel krn4,4,YVT
	HCLSetKernel krn4,5,YUT

	HCLSetKernel krn10,0,YUN
	HCLSetKernel krn10,1,GXU
	HCLSetKernel krn10,2,GYU
	HCLSetKernel krn10,3,YU
	HCLSetKernel krn10,4,YVU
	HCLSetKernel krn10,5,GXd0
	HCLSetKernel krn10,6,GYd0
	HCLSetKernel krn10,7,Yd0
	HCLSetKernel krn10,8,DT
	HCLSetKernel krn10,9,kabeX

	HCLSetKernel krn11,0,YVN
	HCLSetKernel krn11,1,GXV
	HCLSetKernel krn11,2,GYV
	HCLSetKernel krn11,3,YUV
	HCLSetKernel krn11,4,YV
	HCLSetKernel krn11,5,GXd1
	HCLSetKernel krn11,6,GYd1
	HCLSetKernel krn11,7,Yd1
	HCLSetKernel krn11,8,DT
	HCLSetKernel krn11,9,kabeY

	HCLSetKernel krn12,0,YPN
	HCLSetKernel krn12,1,GXP
	HCLSetKernel krn12,2,GYP
	HCLSetKernel krn12,3,YUT
	HCLSetKernel krn12,4,YVT
	HCLSetKernel krn12,5,GXd2
	HCLSetKernel krn12,6,GYd2
	HCLSetKernel krn12,7,Yd2
	HCLSetKernel krn12,8,DT
	HCLSetKernel krn12,9,kabeP
	
	HCLSetKernel kout,0,out
	HCLSetKernel kout,1,RYS
	HCLSetKernel kout,2,YU
	HCLSetKernel kout,3,YV

	HCLSetKernel ryuusi,0,RYS
	HCLSetKernel ryuusi,1,YUN
	HCLSetKernel ryuusi,2,YVN
	HCLSetKernel ryuusi,3,DT
	HCLSetKernel ryuusi,4,GXU
	HCLSetKernel ryuusi,5,GYU
	HCLSetKernel ryuusi,6,GXV
	HCLSetKernel ryuusi,7,GYV

	HCLSetKernel kout0,0,out
	return
	