/////////////////////////////////////////////////////////////////////////////////////////////////
//倍精度で高速フーリエ変換
//Nサイズが大きい場合
/////////////////////////////////////////////////////////////////////////////////////////////////
#include "HSPCL64.as"
	HCLinit
	randomize
	M=24//冪指数。メモリにおさまる範囲で28あたりがmaxか
	N=1<<M
	
	prgid=HCLCreateProgram("FFT.cl")
	krnfft=HCLCreateKernel(prgid,"FFT")
	krnBitRev=HCLCreateKernel(prgid,"BitRev")

	
	ddim hs_A,N*2
		repeat N*2
		hs_A.cnt=0.0001*rnd(30000)
		loop
	memA=HCLCreateBufferFrom(hs_A)//実数と虚数があり、1つの数字は8byte
	//初期設定終わり

	
	HCLSetKrns krnfft,memA,M
	HCLSetKrns krnBitRev,memA,M
	//GPUでFFTの部分/////////////////////////////////////////////////////////
		repeat M
		HCLSetKernel krnfft,2,cnt
		HCLDoKrn1 krnfft,N/2,limit(N/2,1,128)
		loop
	HCLDoKrn1 krnBitRev,N,limit(N,1,128)
	HCLFinish
	//GPUでFFTの部分/////////////////////////////////////////////////////////

	
	pos 0,0
	mes "GPU\n実数		虚数"
	pos 0,38
	ansviewnum=limit(N,1,9)
		repeat ansviewnum
		mes ""+HCLReadIndex_dp(memA,cnt*2)+"	"+HCLReadIndex_dp(memA,cnt*2+1)
		loop

	gosub*DFT検算
	stop




*DFT検算
	//DFT計算。二重ループ
	
	ddim ret_r,ansviewnum
	ddim ret_i,ansviewnum
		repeat ansviewnum
		t=cnt
			repeat N
			x=cnt
			w=-3.14159265358979323846264338328*2.0*t*x/N

			s=sin(w)
			c=cos(w)

			r=hs_A(x*2  )
			i=hs_A(x*2+1)

			ret_r.t+=r*c-i*s
			ret_i.t+=r*s+i*c
			loop
		loop
	
	pos 0,220
	mes "CPUで検算(DFT)\n実数		虚数"
	pos 0,258
		repeat ansviewnum
		mes ""+ret_r(cnt)+"	"+ret_i(cnt)
		loop
	return