/////////////////////////////////////////////////////////////////////////////////////////////////
;共有メモリのサンプル
;65536*64個のランダムな数をGPUに仕分けさせる

;1スレッドあたり64個の数を抜き出して、偶数、奇数の数を割り出す
;このスレッドを65536個回すのだが、ローカルアイテムサイズを256にして共有メモリ(ローカルメモリ)を使うことで
;256スレッド同士での数値の共有が可能になる
;つまり65536スレッドを256スレッド同士つなげて256要素のint型配列変数に奇数、偶数の数を保存
;この256にまとまった偶数合計、奇数合計をVRAMからCPUに転送して256回加算して最終的な合計値を求める
/////////////////////////////////////////////////////////////////////////////////////////////////



randomize
#include "hspcl32.as"
	HCLini 1
	if HCLDevCount=0:dialog "対応ビデオカードがありません。終了します",1:end

	HCLSetDev 0;0番目のデバイスを使って計算する
	mes "65536個のスレッドを実行します。\n各々のスレッドで64個の数値を偶数奇数カウントしていきます。\nこのときカーネル内で256個のスレッドが同期して情報を共有します\n"
	dim rando,65536*64
		repeat 65536*64;419万個のランダム数生成
		rando.cnt=rnd(32767)*rnd(32000)+rnd(32767)
		loop
	mes "65536*64個のランダム数生成"

	HCLBuildProgram "共有メモリサンプル、奇数偶数分けその2.cl",prgid;
	HCLCreateKernel prgid,"wake",krnid;
	HCLCreateBuffer vram,65536*64*4;
	mes "65536*64個のint型メモリ確保"
	HCLCreateBuffer kisu,256*4;奇数の数を保存するint型配列変数をVRAMに作成
	HCLCreateBuffer gusu,256*4;偶数の数を保存するint型配列変数をVRAMに作成
	HCLSetKernel krnid,0,vram;
	HCLSetKernel krnid,1,0,4*256;奇数用ローカルメモリ作成。１ワークグループにつき1024byte作成
	HCLSetKernel krnid,2,0,4*256;偶数用ローカルメモリ作成。１ワークグループにつき1024byte作成
	HCLSetKernel krnid,3,kisu;
	HCLSetKernel krnid,4,gusu;
	mes "データ転送"
	HCLWriteBuffer vram,rando,65536*64*4;
	HCLWaitTask




	//GPU側で計算開始！
	mes "カーネル実行中"
		timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)
	HCLDoKrn1 krnid,65536*64/64,256;1スレッドあたり64個のint変数を仕分け、またローカルワークサイズが256なのがミソ！！
	HCLWaitTask
		timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)-timeee

	//CPU側で集計
	dim k,256
	HCLReadBuffer kisu,k,1024;vram → k  に65536byteコピー
	奇数の数=0
		repeat 256
		奇数の数+=k.cnt
		loop
	HCLReadBuffer gusu,k,1024;vram → k  に65536byteコピー
	偶数の数=0
		repeat 256
		偶数の数+=k.cnt
		loop


	//表示
	mes "\nGPUでカウントした結果 ("+timeee+"ms)"
	mes "奇数の数		"+奇数の数
	mes "偶数の数		"+偶数の数
	mes "奇数の数＋数の数＝	"+(偶数の数+奇数の数)+"    =4194304なら正確にできた"


	//CPU側で計算開始
		timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)
	repeat 65536*64;419万個ループ
		if rando.cnt\2:intkisuko++:else:intgusuko++
	loop
		timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)-timeee

	//表示
	mes "\nCPUでカウントした結果 ("+timeee+"ms)"
	mes "奇数の数		"+intkisuko
	mes "偶数の数		"+intgusuko
	mes "奇数の数＋偶数の数＝	"+(intkisuko+intgusuko)+"    =4194304なら正確にできた"

	HCLbye
	stop