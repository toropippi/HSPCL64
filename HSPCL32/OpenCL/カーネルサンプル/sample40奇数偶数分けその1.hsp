/////////////////////////////////////////////////////////////////////////////////////////////////
;カーネル内でifやfor(ループ)を使うサンプル
;65536*64個のランダムな数をGPUに仕分けさせる

;1スレッドあたり64個の数を抜き出して、偶数、奇数の数を割り出す
;このスレッドを65536個回し、1スレッドがそれぞれ65536要素のuchar型配列変数に奇数、偶数の数を保存
;あとはCPUでそれを65536回加算して奇数偶数の数を割り出す。
/////////////////////////////////////////////////////////////////////////////////////////////////


randomize

#include "hspcl32.as"
	HCLini 1
	if HCLDevCount=0:dialog "対応ビデオカードがありません。終了します",1:end

	HCLSetDev 0;0番目のデバイスを使って計算する
	mes "65536個のスレッドを実行します。\n各々のスレッドで64個の数値を偶数奇数カウントしていきます。\n\n"
	dim rando,65536*64
	intkisuko=0
	intgusuko=0
	mes "65536*64個のランダム数生成中"
		repeat 65536*64;419万個のランダム数生成
		rando.cnt=rnd(32767)*rnd(3200)+rnd(32767)
		loop

	mes "ソースコンパイル"
	HCLBuildProgram "奇数偶数分けその1.cl",prgid : HCLCreateKernel prgid,"wake",krnid;
	mes "VRAMに65536*64個のint型メモリ確保"
	HCLCreateBuffer vram,65536*64*4;
	HCLCreateBuffer kisu,65536;奇数の数を保存するchar型配列変数を作成
	HCLCreateBuffer gusu,65536;偶数の数を保存するchar型配列変数を作成
	HCLSetKernel krnid,0,vram;
	HCLSetKernel krnid,1,kisu;
	HCLSetKernel krnid,2,gusu;
	mes "データ転送"
	HCLWriteBuffer vram,rando,65536*64*4;
	HCLWaitTask


	//GPU側で計算開始！
	timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)
	mes "カーネル実行中"
	HCLDoKrn1 krnid,65536*64/64,64;1スレッドあたり64個のint変数を仕分け,ローカルワークサイズは適当な数字でOK
	HCLWaitTask;（これがないと計算中なのにHCLReadBufferで計算途中のものを読み込み正しい答えがでない場合がある。またこの場合時間を計測するためにHCLWaitTaskを使っている）
	mes "カーネル実行終了"
	timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)-timeee

	//GPUからデーター取りだし集計
	sdim k,65536
	HCLReadBuffer kisu,k,65536;vram → k  に65536byteコピー
	奇数の数=0
	repeat 65536
		奇数の数+=peek(k,cnt)
	loop

	HCLReadBuffer gusu,k,65536;vram → k  に65536byteコピー
	偶数の数=0
	repeat 65536
		偶数の数+=peek(k,cnt)
	loop

	//表示
	mes "\nGPUでカウントした結果 ("+timeee+"ms)"
	mes "奇数の数		"+奇数の数
	mes "偶数の数		"+偶数の数
	mes "奇数の数＋偶数の数＝	"+(偶数の数+奇数の数)+"    =4194304なら正確にできた"

	//CPU側で計算開始
	timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)
	repeat 65536*64;419万個ループ
		if rando.cnt\2:intkisuko++:else:intgusuko++
	loop
	timeee=gettime(5)*60*1000+gettime(6)*1000+gettime(7)-timeee

	//表示
	mes "\nCPUでカウントした結果 ("+timeee+"ms)"	
	mes "奇数の数		"+intkisuko
	mes "偶数の数		"+intgusuko
	mes "奇数の数＋偶数の数＝	"+(intkisuko+intgusuko)+"    =4194304なら正確にできた"

	HCLbye
	stop